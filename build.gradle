buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2'
    }
}

apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'java'
apply plugin: 'idea'

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

testSets {
    testInfrastructure {
        dirName = 'test-infrastructure'
    }

    integrationTest {
        dirName = 'test-integration'
        extendsFrom testInfrastructure
    }
}
integrationTest.outputs.upToDateWhen { false }

check.dependsOn integrationTest
integrationTest.mustRunAfter test

dependencies {
    compile 'commons-io:commons-io:2.5'
    compile 'com.google.guava:guava:20.0'
    compile 'com.github.matejtymes:javafixes:1.2.0.rc1'

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'ch.qos.logback:logback-classic:1.1.9'

    compile 'org.mongodb:mongo-java-driver:3.5.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
//    testCompile 'com.h2database:h2:1.4.196'

    testInfrastructureCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.0'
    integrationTestCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.0'

    testCompile sourceSets.testInfrastructure.output
    integrationTestCompile sourceSets.testInfrastructure.output
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

tasks.withType(Test) {
    reports.html.setDestination(file("${reporting.baseDir}/${name}"))
}